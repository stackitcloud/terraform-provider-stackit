---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "{{.Name}} {{.Type}} - {{.ProviderName}}"
subcategory: ""
description: |-
  {{ .Description | trimspace }}
---

# {{.Name}} ({{.Type}})

{{ .Description | trimspace }}

## Example Usage

{{ tffile (printf "examples/resources/%s/resource.tf" .Name) }}

## Migration of IaaS resources from versions <= v0.78.1

The release of the STACKIT IaaS API v2 provides a lot of new features, but also includes some breaking changes
(when coming from v1 of the STACKIT IaaS API) which must be somehow represented on Terraform side. The
`stackit_network_area` resource deprecated some fields. See the example below how to migrate your resources.

### Migration: Network area resource (stackit_network_area)

**Configuration for <= v0.78.1**

```terraform
resource "stackit_network_area" "example" {
  organization_id  = "xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
  name             = "my-network-area"
  transfer_network = "192.168.1.0/24" # transfer_network field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
  network_ranges = [
    {
      prefix = "192.168.0.0/25" # network_ranges field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
    }
  ]
  default_nameservers   = ["8.8.8.8"] # default_nameservers field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
  default_prefix_length = 25 # default_prefix_length field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
  max_prefix_length     = 29 # max_prefix_length field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
  min_prefix_length     = 24 # min_prefix_length field got deprecated for provider versions > v0.74.0, use the new "stackit_network_area_region" resource instead
}
```
The previous configuration has some deprecated fields.

**Configuration for > v0.78.1**

To migrate from a previous state, you need to modify your configuration like this:

```terraform
resource "stackit_network_area" "example" {
  organization_id = "xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
  name            = "my-network-area"
  # remove all the deprecated fields
}

# Add the new resource "stackit_network_area_region" and configure it with all the deprecated values from the "stackit_network_area" resource
resource "stackit_network_area_region" "example" {
  organization_id = "xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx"
  network_area_id = stackit_network_area.example.network_area_id
  region = "eu01"
  ipv4 = {
    transfer_network = "192.168.1.0/24"
    network_ranges = [
      {
        prefix = "192.168.0.0/25"
      }
    ]
    default_nameservers   = ["8.8.8.8"]
    default_prefix_length = 25
    max_prefix_length     = 29
    min_prefix_length     = 24
  }
}

# Add this import for "stackit_network_area_region" to migrate from the previous configuration
import {
  to = stackit_network_area_region.example
  id = "${stackit_network_area.example.id},eu01"
}
```

After modifying the configuration, run `$ terraform plan` to check what terraform will do.
The changes should trigger an update in-place for the existing "stackit_network_area" where the deprecated fields will be removed and the resource "stackit_network_area_region" should be imported.
It shouldn't trigger any recreation. If terraform wants to recreate any of the resources, verify that you are using the provider version > v0.78.1 and have everything defined correctly.

When everything looks good, run `$ terraform apply` to apply these changes.
When the run is completed, you can remove the import-block. Run `$ terraform plan` to verify that the infrastructure matches the configuration.

{{ .SchemaMarkdown | trimspace }}
